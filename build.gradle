buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
}

version "1.0.0.M3"
group "org.grails.plugins"

apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    console "org.grails:grails-console"
    compile 'io.dropwizard.metrics:metrics-core:3.1.2'
    profile "org.grails.profiles:web-plugin"
    testCompile "org.grails:grails-plugin-testing"
}

asciidoctor {
    attributes 'experimental'  : 'true',
            'compat-mode'   : 'true',
            'toc'           : 'left',
            'icons'         : 'font',
            'version'       : project.version
}

// enable if you wish to package this plugin as a standalone application
bootRepackage.enabled = false
grailsPublish {
    userOrg = 'grails'
    githubSlug = 'grails-plugins/grails-dropwizard-metrics'
    license {
        name = 'Apache-2.0'
    }
    title = 'Grails Dropwizard Metrics'
    desc = 'Monitoring And Metrics Plugin For Grails 3'
    developers = [jeffbrown: "Jeff Scott Brown"]
}

groovydoc.docTitle = "Grails Dropwizard Metrics ${project.version}"
groovydoc.destinationDir = new File(buildDir, 'docs/api')

asciidoctor.outputDir = new File(buildDir, 'docs')
asciidoctor.separateOutputDirs = false

task docs(dependsOn:[groovydoc, asciidoctor])
